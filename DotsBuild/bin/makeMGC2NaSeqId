#!@perl@ 

use strict;
use lib "$ENV{GUS_HOME}/lib/perl";
use Getopt::Long;
use GUS::ObjRelP::DbiDatabase;
use GUS::Common::GusConfig;


my ($verbose, $inputFile, $taxon_id, $gusConfigFile, $regex);
&GetOptions("verbose!"=> \$verbose,
            "inputFile=s" => \$inputFile,
	    "taxon_id=i" => \$taxon_id,
            "gusConfigFile=s" => \$gusConfigFile,
            "regex=s" => \$regex);

my $usage = "
usage: makeMGC2DoTSfile --inputFile  --taxon_id --verbose --gusConfigFile [\$GUS_CONFIG_FILE]\n

Create mapping between MGC and DoTS.  

Input is a 8 column tab delimited file, where each row is: 
symbol\tGenbank_defline(MGC:id)\tLL_id\tunigene_id\timage_id\tGenbank_acc\tlibrary_id\tlibrary_name

Output is 2 column space delimited, where each row is:
MGC:id\tdots.externalnasequence.na_sequence_id
";

die $usage unless ($inputFile && $taxon_id);

print STDERR "Establishing dbi login\n" if $verbose;

my %mgc2naseqid = &parseMGCFile($inputFile);

my $gusconfig = GUS::Common::GusConfig->new($gusConfigFile);

my $db = GUS::ObjRelP::DbiDatabase->new($gusconfig->getDbiDsn(),
					$gusconfig->getReadOnlyDatabaseLogin(),
					$gusconfig->getReadOnlyDatabasePassword(),
					$verbose,0,1,
					$gusconfig->getCoreSchemaName());

my $dbh = $db->getQueryHandle();

my $sql = "select na_sequence_id from dots.ExternalNASequence where source_id = ? and taxon_id = $taxon_id";

print STDERR "Query: $sql\n" if $verbose;

my $stmt = $dbh->prepare($sql);

my $c;

foreach my $mgc (keys %mgc2naseqid) {
  $stmt->execute($mgc2naseqid{$mgc});
  my ($id) = $stmt->fetchrow_array();
  print "$mgc\t$id\n" if $id;
  $stmt->finish();
  $c++;
  print STDERR "Processed $c\n" if $verbose && $c % 1000 == 0;
}

$db->logout();

sub parseMGCFile {
    my ($file) = @_;

    open(F, $file) || die "Can't open input file $file";

    my %mgc2naseqid;
    while(<F>) {
      chomp;
      my $mgc;
      my @line = split(/\t/,$_);
      if ($line[1] =~ /(MGC:\d+)/) {
	$mgc = $1;
      }
      else {
	next;
      }
      my $acc = $line[5];
      $acc =~ s/\.\d+//;
      $mgc2naseqid{$mgc}=$acc;
    }
    return %mgc2naseqid;
}
